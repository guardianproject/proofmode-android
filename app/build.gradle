plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.ksp)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.kotlin.compose)
}


android {
    compileSdk 36
    namespace = "org.witness.proofmode"

    buildFeatures {
        viewBinding = true
    }

    flavorDimensions = ['default']

    productFlavors {
        artwork {
            dimension 'default'
        }
    }


    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "org.witness.proofmode"
        minSdkVersion 28
        targetSdkVersion 36
        versionCode 300301
        versionName "3.0.0-RC-1"
        multiDexEnabled = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        resources {
            excludes += ['org.bitcoin.production.checkpoints', 'org.bitcoin.test.checkpoints', 'org/bitcoinj/crypto/cacerts', 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt', 'lib/x86_64/darwin/libscrypt.dylib', 'com/google/thirdparty/publicsuffix/PublicSuffixType.gwt.xml', 'com/google/thirdparty/publicsuffix/PublicSuffixPatterns.gwt.xml', 'com/google/thirdparty/publicsuffix/PublicSuffixPatterns.gwt.xml', 'org/apache/commons/cli/AlreadySelectedException.class','META-INF/INDEX.LIST','META-INF/io.netty.versions.properties',"META-INF/versions/9/OSGI-INF/MANIFEST.MF"]
        }
    }
    lint {
        abortOnError = false
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding = true
        compose = true
    }
    configurations {
        all {
            resolutionStrategy {
                // do not upgrade above 3.12.0 to support API < 21 while server uses
                // COMPATIBLE_TLS, or okhttp3 is used in project
               //force 'com.squareup.okio:okio:3.2.0'
            }
        }
    }


    splits {
        abi {
            enable = true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk = true
        }
    }

}

//Increments versionCode by ABI type
def abiCodeMap = ['armeabi-v7a': 1, 'arm64-v8a': 2, 'x86': 4, 'x86_64': 5]
androidComponents {
    onVariants(selector().all()) { variant ->
        variant.outputs.each { output ->
            def abi = output.filters.find { it.filterType.toString() == 'ABI' }?.identifier
            def abiCode = abiCodeMap.get(abi)
            if (abiCode != null) {
                output.versionCode.set(output.versionCode.get() * 100 + abiCode)
            }
        }
    }
}

base {
    archivesName = "Proofmode-${android.defaultConfig.versionName}"
}

dependencies {
    implementation libs.bundles.navigation

    implementation libs.kotlinx.coroutines.android
    implementation libs.okhttp

    testImplementation libs.junit

    implementation project(':android-libproofmode')
    implementation project(':android-opentimestamps')

    implementation libs.androidx.core.ktx
    implementation libs.androidx.preference.ktx

    // Activity view
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.runtime
    implementation libs.androidx.compose.material3
    implementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.constraintlayout.compose
    implementation libs.androidx.activity.compose
    implementation libs.coil.compose
    implementation libs.coil.video
    implementation libs.kotlinx.serialization.json

    // Room database support (for activity view)
    implementation libs.bundles.room
    // Using KSP instead of KAPT for better performance
    ksp libs.androidx.room.compiler

    implementation project(":android-libproofcam")
    implementation libs.google.material
    implementation libs.androidx.compose.material.icons.core

    implementation libs.androidx.constraintlayout
    implementation libs.androidx.legacy.support.v4
    implementation libs.androidx.legacy.support.v13


    //To be removed
    implementation libs.appintro

    implementation libs.timber

    implementation libs.androidsvg

    implementation libs.tedimagepicker

//    implementation 'com.google.android.gms:play-services-safetynet:18.0.1'
    implementation libs.listenablefuture


    // Required -- JUnit 4 framework
    androidTestImplementation libs.junit

    // Testing-only dependencies
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.espresso.core

    //crash reporting
    implementation libs.bundles.acra

    //background service worker
    implementation libs.bundles.work

    implementation libs.zxing.core



}
